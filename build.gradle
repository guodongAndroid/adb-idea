plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.jetbrains.intellij" version "1.6.0"
}

repositories {
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/releases'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "1.6.6.1"

sourceSets {
    main {
        resources {
            exclude 'website/adb_operations_popup.png'
            exclude 'website/find_actions.png'
            exclude 'website/debug_howto.png'
        }
    }
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    pluginName = 'adb_idea'
    updateSinceUntilBuild = false

//    version '203.8084.24'
    localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath

    instrumentCode {
        compilerVersion = "203.7717.56"
    }

    instrumentTestCode {
        compilerVersion = "203.7717.56"
    }
}

group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.21"
    implementation "org.jooq:joor-java-8:0.9.14"
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation "com.google.truth:truth:1.1.3"
}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}


compileJava.dependsOn verifySetup
